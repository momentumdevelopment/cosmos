buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public" }
        maven { url = "https://plugins.gradle.org/m2" }
        maven { url = "https://libraries.minecraft.net" }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.1.0"
group = "cope.cosmos"
archivesBaseName = "cosmos"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

jar.manifest.attributes(
        'FMLCorePluginContainsFMLMod': true,
        'FMLCorePlugin': 'cope.cosmos.asm.Mixinloader',
        'ForceLoadAsMod': true,
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
)

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"
    coreMod = "cope.cosmos.asm.MixinLoader"
    makeObfSourceJar = false
}

repositories {
    jcenter()
    mavenCentral()

    maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url = 'https://jitpack.io'}
}

dependencies {
    compile "club.minnced:java-discord-rpc:2.0.1"
    compile "com.mojang:brigadier:1.0.17"
    compile "com.moandjiezana.toml:toml4j:0.7.2"
    compile ('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core' 
    }
}

mixin {
    add sourceSets.main, 'mixins.cosmos.refmap.json'
}

shadowJar {
    dependencies {
        include(dependency('club.minnced:java-discord-rpc:2.0.1'))
        include(dependency('org.spongepowered:mixin'))
        include(dependency('com.mojang:brigadier:1.0.17'))
        include(dependency('com.moandjiezana.toml:toml4j:0.7.2'))
    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = ''
}

build.dependsOn(shadowJar)

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
